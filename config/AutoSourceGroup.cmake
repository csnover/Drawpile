function(target_auto_source_group)
	foreach(target IN LISTS ARGN)
		get_target_property(sources ${target} SOURCES)
		get_target_property(resources ${target} RESOURCE)
		foreach(filename IN LISTS sources)
			if(filename MATCHES "\\.[Hh]([Pp][Pp])?$" OR filename MATCHES "include[\\/][^.]*$")
				set(prefix "Header Files")
			elseif(filename MATCHES "\\.[Cc]([Pp][Pp])?$")
				set(prefix "Source Files")
			else()
				set(prefix "Resources")
			endif()

			string(FIND "${filename}" "${CMAKE_CURRENT_BINARY_DIR}" bin_dir_pos)
			if(bin_dir_pos EQUAL 0)
				source_group("${prefix}" FILES ${filename})
			else()
				source_group(TREE "${CMAKE_CURRENT_SOURCE_DIR}" PREFIX "${prefix}" FILES ${filename})
			endif()
		endforeach()
	endforeach()
endfunction()
